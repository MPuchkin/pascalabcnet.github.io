---
title: PascalABC.NET и ЕГЭ по информатике
keywords: styles
last_updated: 07.01.2020
sidebar: mydoc_sidebar
permalink: mydoc_for_EGE.html
toc: false
folder: mydoc
---

PascalABC.NET - современный диалект языка программирования Паскаль, позволяющий записывать код компактно и понятно, используя современные языковые возможности. 

Использование языковых возможностей PascalABC.NET позволяет записывать программу яснее и как следствие сократить число возможных ошибок на экзамене ЕГЭ, связанных с волнением и другими субъективными причинами.

Данный текст составлен разработчиками языка и рассматривает ряд вопросов, связанных с использованием PascalABC.NET при сдаче ЕГЭ по информатике. Он ориентирован:

* на школьников, использующих при сдаче ЕГЭ PascalABC.NET как язык реализации программ
* на преподавателей, которые при подготовке школьников к сдаче ЕГЭ по информатике используют PascalABC.NET
* на проверяющих ЕГЭ (данный текст можно использовать как справочник по конструкциям, отсутствующим в стандартном Паскале) 

В частности, ***данный текст может быть использован при подаче апелляций***, если есть твёрдая уверенность в том, что оценка незаслуженно снижена из-за использования правильных языковых средств PascalABC.NET.

Прежде всего, речь идёт только о задачах 25 и 27, где требуется написать текст программы на языке программирования (рассматривается [Демо ЕГЭ по информатике 2020](https://4ege.ru/index.php?do=download&id=10464)). Причем, в задаче 25 уже дан код на стандартном языке Паскаль, но сказано, что можно реализовывать на другом языке, используя те же самые исходные данные и переменные, что приведены в модельном решении.

***Самое главное***: в каждом из заданий 25 и 27 следует указать язык и версию реализованной программы. Здесь в начале решения следует написать "Используется версия PascalABC.NET 3.6.3 или выше" - это снимет все вопросы у добросовестного проверяющего.

Отметим, что только в задаче 27 требуется эффективность программы по времени и по памяти, в задаче 25 этого не требуется.

При решении задач ЕГЭ по информатике мы рекомендуем использовать возможности PascalABC.NET, которые используются практически в каждой программе, написанной на PascalABC.NET, и делающие текст программы яснее и короче. К этим возможностям относятся:

1. Описания переменных внутри блока в том месте, где они впервые потребовались. Это ликвидирует длинные перечни описания переменных до beginа основной программы, ухудшающие читаемость и лёгкость написания программы.
2. Автовывод типа переменной при описании с инициализацией.
3. Использование описания счётчика цикла for в заголовке цикла (for var i).
4. Функции ввода вида `ReadInteger`, `ReadReal`, `ReadInteger2` и т.д., позволяющие одной строкой описывать и вводить переменную в любом месте операторного блока программы.
5. Процедуры вывода Print, Println, автоматически разделяющие элементы вывода пробелами.
6. Цикл loop - аналог цикла for, использующийся когда счётчик цикла не нужен.
7. Кортежи и распаковка кортежей в переменные, называемая также или множественным присваиванием: (a,b) := (1,1).
8.

Для того чтобы сразу продемонстрировать преимущества использования PascalABC.NET перед стандартным Паскалем, рассмотрим 2 решения задачи 27.

***Решение 1***. (предлагается в тексте Демо ЕГЭ по информатике 2020 как эталонное)

```pascal
const
  m = 120; {количество различных остатков}

var
  {хранение максимального значения для каждого из остатков}
  r: array[0..m - 1] of integer;
  n, a, i, p, left, right: integer;

begin
  readln(n);
  {обнуление массива r}
  for i := 0 to m - 1 do
    r[i] := 0;
  {обнуление переменных для записи ответа}
  left := 0; right := 0;
  {ввод значений, поиск искомой пары}
  for i := 1 to n do
  begin
    readln(a); {считываем очередное значение}
    p := a mod m;
    if p = 0 then
    begin
      if (r[0] > a) and (r[0] + a > left + right) then
      begin
        left := r[0]; right := a {обновление ответа}
      end
    end
    else
    begin
      if (r[m - p] > a) and (r[m - p] + a > left + right) then
      begin
        left := r[m - p]; right := a {обновление ответа}
      end
    end;
    {обновление элемента r для соответствующего остатка}
    if a > r[p] then r[p] := a
  end;
  writeln(left, ' ', right)
end.
```

***Решение 2***. Использует возможности PascalABC.NET

```pascal
// PascalABC.NET версии 3.6.3
const m = 120;

begin
  var r := ArrFill(m,0); 
  var (left,right) := (0,0);
  
  var n := ReadInteger;
  loop n do
  begin
    var a := ReadInteger;
    var p := a mod m;
    var l := if p = 0 then r[0] else r[m - p];

    if (l > a) and (l + a > left + right) then
        (left, right) := (l, a);

    if a > r[p] then r[p] := a
  end;
  Println(left, right)
end.
```

Решение на PascalABC.NET полностью идентично эталонному, но оно в два раза более короткое, проще воспринимается и имеет меньшую вероятность совершить ошибку в стрессовой ситуации экзамена.

Рассмотрим его основные элементы и их отличие от стандартного Паскаля.

1. `ArrFill(m,0)` - стандартная функция, выделяющая память под m элементов динамического массива, заполняющего его нулями и возвращающая ссылку на него. 

2. В операторе `var a := ArrFill(m,0)` описывается и инициализируется массив a, при этом тип a автоматически выводится компилятором: это `array of integer`.

3. В записи `var (left,right) := (0,0);` описываются сразу две переменные целого типа, и они инициализируются нулями (используется так называемая распаковка кортежа в переменные при инициализации).

4. Описание целой переменной n и инициализация её целым значением, вводимым с клавиатуры, осуществляется оператором `var n := ReadInteger;`

5.



{% include links.html %}
